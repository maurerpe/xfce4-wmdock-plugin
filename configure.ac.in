dnl
dnl xfce4-wmdock-plugin - A wm dockapp plugin for the Xfce panel
dnl
dnl 2021 Paul Maurer
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([wmdock_version_major], [0])
m4_define([wmdock_version_minor], [8])
m4_define([wmdock_version_micro], [0])
m4_define([wmdock_version_nano],  []) dnl leave this empty to have no nano version
m4_define([wmdock_version_build], [@REVISION@])
m4_define([wmdock_version_tag], [git])
m4_define([wmdock_version], [wmdock_version_major().wmdock_version_minor().wmdock_version_micro()ifelse(wmdock_version_nano(), [], [], [.wmdock_version_nano()])ifelse(wmdock_version_tag(), [git], [wmdock_version_tag()-wmdock_version_build()], [wmdock_version_tag()])])
m4_define([wmdock_default_debug], [ifelse(wmdock_version_tag(), [git], [yes], [minimum])])

AC_CONFIG_MACRO_DIR([m4])
dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (C) 2021 Paul Maurer])
AC_INIT([xfce4-wmdock-plugin], [wmdock_version], [https://github.com/maurerpe/xfce4-wmdock-plugin], [xfce4-wmdock-plugin])
AC_PREREQ([2.50])
AC_REVISION([xfce4_panel_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.15 no-dist-gzip dist-bzip2 tar-ustar foreign])
AC_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([0.35.0])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([stdlib.h unistd.h locale.h stdio.h errno.h time.h string.h \
                  math.h sys/types.h sys/wait.h memory.h signal.h sys/prctl.h \
                  libintl.h])
AC_CHECK_FUNCS([bind_textdomain_codeset])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl *******************************
dnl *** Check for X11 installed ***
dnl *******************************
XDT_CHECK_LIBX11_REQUIRE()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.14.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.14.0])
XDT_CHECK_PACKAGE([LIBWNCK], [libwnck-3.0], [3.0.0])
XDT_CHECK_PACKAGE([X11], [x11])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([wmdock_default_debug])

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

AC_CONFIG_FILES([
Makefile
icons/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
panel-plugin/Makefile
po/Makefile.in
])
AC_OUTPUT

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:    $enable_debug"
echo
